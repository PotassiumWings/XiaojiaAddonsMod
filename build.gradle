buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "jitpack"
            url = "https://jitpack.io/"
        }
        maven { url = "https://repo.spongepowered.org/maven" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.github.CCBlueX:ForgeGradle:a3d86a5"
        classpath "com.github.LxGaming:MixinGradle:d75e32e"
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: "com.github.johnrengelman.shadow"

version = "2.5.2"
group= "com.xiaojia.xiaojiaaddons"
archivesBaseName = "XiaojiaAddons"
compileJava.options.encoding = "UTF-8"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
}

repositories {
    jcenter()
    maven { url 'https://repo.spongepowered.org/maven/' }
}

dependencies {
    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    compile fileTree(dir: 'lib', includes: ['*.jar'])
}

mixin {
    add sourceSets.main, "mixins.xiaojiaaddons.refmap.json"
}

shadowJar {
    dependencies {
        exclude 'gson-2.2.4.jar'
        exclude 'guava-17.0.jar'
        exclude 'commons-io-2.4.jar'
        exclude 'dummyThing'
        exclude 'LICENSE.txt'
    }
    finalizedBy 'reobfJar'
}

jar {
    manifest.attributes(
            'Main-Class': 'XiaojiaAddons',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.xiaojiaaddons.json',
            'FMLCorePluginContainsFMLMod': 'true',
            "ForceLoadAsMod": true,
            'FMLCorePlugin': 'com.xiaojia.xiaojiaaddons.Mixins.MixinLoader',
    )
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

reobf {
    shadowJar { }
}

sourceJar {
    enabled = false
}